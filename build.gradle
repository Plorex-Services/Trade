import it.bitrule.plorex.script.ASpotlessPlugin
import it.bitrule.plorex.script.CommonPlugin
import it.bitrule.plorex.script.PublishPlugin

plugins {
    id 'maven-publish'
    id 'java'
    id 'idea'
    alias libs.plugins.shadow
    alias libs.plugins.bukkitPluginYAML
}

apply plugin: "java"
apply plugin: ASpotlessPlugin
apply plugin: PublishPlugin
apply plugin: CommonPlugin

repositories {
    mavenLocal()

    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    implementation 'dev.triumphteam:triumph-gui:3.1.12'

    compileOnly(libs.annotations.get())

    compileOnly(libs.lombok.get())
    annotationProcessor(libs.lombok.get())

    testCompileOnly(libs.lombok.get())
    testAnnotationProcessor(libs.lombok.get())
}

tasks {
    build {
        dependsOn spotlessApply
        dependsOn shadowJar
    }

    shadowJar {
        archiveFileName.set("${rootProject.name}-${version}.jar")
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-Xpkginfo:always")
    }
}

//Automatically download dependencies source code
idea {
    module {
        downloadSources = true
        downloadJavadoc = false
    }
}

tasks.withType(AbstractCopyTask).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

java {
    withSourcesJar()
}

bukkit {
    main = "it.bitrule.trade.Trade"
    apiVersion = "1.21.5"
    version = "${project.version}"
    authors = ["bitrule"]
    description = "${findProperty("plugin-description")}"
    name = "${findProperty("plugin-name")}"
}